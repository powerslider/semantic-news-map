PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/>
PREFIX pub-old: <http://ontology.ontotext.com/publishing#>
PREFIX pub: <http://ontology.ontotext.com/taxonomy/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX ff-map: <http://factforge.net/ff2016-mapping/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?relativeEntity (max(?name) as ?entityLabel)
(xsd:integer(round((?relWeight1 + ?relWeight2) * 50 + ?relWeight3)) as ?relativePopularity)
(sum(?rel_weight1) as ?relWeight1) (sum(?c1) as ?count1)
(sum(?rel_weight2) as ?relWeight2) (sum(?c2) as ?count2)
(sum(?rel_weight3) as ?relWeight3)
FROM onto:disable-sameAs
WHERE {
    {
        SELECT DISTINCT ?relativeEntity (sum(?weight) as ?rel_weight1) (count(*) as ?c1)  {
            {
                SELECT DISTINCT ?entity (max(?w1) as ?w2) {
                    ?news pub-old:creationDate ?date .
                    FILTER ((?date > "{min_date}T00:00:00Z"^^xsd:dateTime) && (?date < "{max_date}T23:59:59Z"^^xsd:dateTime))
                    ?news pub-old:category ?category .
                    FILTER (?category = "{category}"^^xsd:string)
                    ?news pub-old:containsMention ?mention .
                    ?mention pub-old:hasInstance ?pubEntity ;
                        pub-old:relevanceScore ?relevance .
                    ?pubEntity pub:exactMatch ?entity .
                    ?entity a dbo:Agent .
                    BIND(?relevance as ?w1)
                } group by ?entity ?news
            }
            ?entity ff-map:agentRelation ?e2 ;
                ff-map:outDegree ?degree .
            BIND(?e2 as ?relativeEntity)
            BIND((?w2 / ?degree * "1.0"^^xsd:double) as ?weight)
        } group by ?relativeEntity ?entity
    }
    UNION
    {
        SELECT DISTINCT ?relativeEntity (sum(?weight) as ?rel_weight2) (count(*) as ?c2) {
            {
                SELECT DISTINCT ?entity (max(?w1) as ?w2) {
                    ?news pub-old:creationDate ?date .
                    FILTER ((?date > "{min_date}T00:00:00Z"^^xsd:dateTime) && (?date < "{max_date}T23:59:59Z"^^xsd:dateTime))
                    ?news pub-old:category ?category .
                    FILTER (?category = "{category}"^^xsd:string)
                    ?news pub-old:containsMention ?mention .
                    ?mention pub-old:hasInstance ?pubEntity ;
                             pub-old:relevanceScore ?relevance .
                    ?pubEntity pub:exactMatch ?entity .
                    ?entity a dbo:Agent .
                    BIND(?relevance as ?w1)
                } group by ?entity ?news
            }
            ?entity ff-map:agentRelation ?e2 ;
                ff-map:outDegree ?degree1 .
            ?e2 ff-map:agentRelation ?e3 ;
                ff-map:outDegree ?degree2 .
            FILTER( ?e3 != ?entity )
            BIND(?e3 as ?relativeEntity)
            BIND((?w2 / ?degree1 / ?degree2 * "1.0"^^xsd:double) as ?weight)
        } group by ?relativeEntity ?entity
    }
    UNION
    {
        #==================================================
        SELECT DISTINCT (?mentionedLodEntity as ?relativeEntity) ?longtermPopularity
        (sum(?entity_relevance) as ?dailyPopularity)
        (xsd:integer(round((?dailyPopularity * ?dailyPopularity / ?longtermPopularity)*100)) AS ?rel_weight3)
        WHERE {
            {
                SELECT ?pubEntity (max(?relevance) as ?entity_relevance)
                {
                    ?news pub-old:containsMention ?mention.
                    ?mention  pub-old:hasInstance ?pubEntity .
                    ?mention pub-old:relevanceScore ?relevance.
                    ?news pub-old:creationDate ?date.
                    FILTER ((?date > "{min_date}T00:00:00Z"^^xsd:dateTime) && (?date < "{max_date}T23:59:00Z"^^xsd:dateTime))
                    FILTER (?pubEntity != <http://ontology.ontotext.com/resource/tslhir2hhreo>)
                    ?news pub-old:category ?category .
                    FILTER (?category = "{category}"^^xsd:string)
                }    GROUP BY ?news ?pubEntity
            }
            ?pubEntity ff-map:longtermPopularity ?longPopularity .
            FILTER (?longPopularity > "1.0"^^xsd:double)
            BIND(IF(?longPopularity < "3.0"^^xsd:double , ?longPopularity * 1.5, ?longPopularity) as ?longtermPopularity)
            ?pubEntity pub:exactMatch ?mentionedLodEntity.
            ?mentionedLodEntity a owl:Thing .
        }
        GROUP BY ?mentionedLodEntity ?longtermPopularity
    } #==================================================
    ?relativeEntity rdfs:label ?name .
}  GROUP BY ?relativeEntity
ORDER BY desc(?relativePopularity)
LIMIT 100