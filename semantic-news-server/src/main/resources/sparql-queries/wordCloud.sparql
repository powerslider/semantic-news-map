PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX pub-old: <http://ontology.ontotext.com/publishing#>
PREFIX pub: <http://ontology.ontotext.com/taxonomy/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/>
PREFIX ff-pub: <http://factforge.org/pub/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ff-map: <http://factforge.net/ff2016-mapping/>

SELECT DISTINCT ?entityLabel ?mentionedLodEntity  ?longtermPopularity
                (sum(?entity_relevance) as ?dailyPopularity)
                (xsd:integer(round((?dailyPopularity * ?dailyPopularity / ?longtermPopularity)*100)) AS ?relativePopularity)
FROM onto:disable-sameAs
WHERE {
    {    SELECT ?pubEntity (max(?relevance) as ?entity_relevance)
        {
            ?news pub-old:containsMention ?mention.
            ?mention  pub-old:hasInstance ?pubEntity .
            ?mention pub-old:relevanceScore ?relevance.

            ?news pub-old:creationDate ?date.
            FILTER ((?date > "{min_date}T00:00:00Z"^^xsd:dateTime) && (?date < "{max_date}T23:59:00Z"^^xsd:dateTime))
            FILTER (?pubEntity != <http://ontology.ontotext.com/resource/tslhir2hhreo>) # CONFIG.SYS: DSP-1404

            ?news pub-old:category ?category .
            FILTER (?category = "{category}"^^xsd:string)

        }    GROUP BY ?news ?pubEntity
     }

     ?pubEntity ff-map:longtermPopularity ?longPopularity .
     FILTER (?longPopularity > "1.0"^^xsd:double)
     BIND(IF(?longPopularity < "3.0"^^xsd:double , ?longPopularity * 1.5, ?longPopularity) as ?longtermPopularity) 

    ?pubEntity pub:preferredLabel ?entityLabel .
    ?pubEntity pub:exactMatch ?mentionedLodEntity.
    ?mentionedLodEntity a owl:Thing .
}
GROUP BY ?mentionedLodEntity ?entityLabel ?longtermPopularity
ORDER BY DESC(?relativePopularity)
LIMIT 100