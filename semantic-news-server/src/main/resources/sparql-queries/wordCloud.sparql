PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX pub-old: <http://ontology.ontotext.com/publishing#>
PREFIX pub: <http://ontology.ontotext.com/taxonomy/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/>
PREFIX ff-pub: <http://factforge.org/pub/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ff-map: <http://factforge.net/ff2016-mapping/>

SELECT DISTINCT ?entity_label ?mentioned_lod_entity  ?longterm_popularity
                (sum(?entity_relevance) as ?daily_popularity)
                (xsd:integer(round((?daily_popularity * ?daily_popularity / ?longterm_popularity)*100)) AS ?relative_popularity)
FROM onto:disable-sameAs
WHERE {
    {    SELECT ?pub_entity (max(?relevance) as ?entity_relevance)
        {
            ?news pub-old:containsMention ?mention.
            ?mention  pub-old:hasInstance ?pub_entity .
            ?mention pub-old:relevanceScore ?relevance.

            ?news pub-old:creationDate ?date.
            FILTER ((?date > "{date}T00:00:00Z"^^xsd:dateTime) && (?date < "{date1}T23:59:00Z"^^xsd:dateTime))
            FILTER (?pub_entity != <http://ontology.ontotext.com/resource/tslhir2hhreo>) # CONFIG.SYS: DSP-1404

            ?news pub-old:category ?category .
            FILTER (?category = "{category}"^^xsd:string)

        }    GROUP BY ?news ?pub_entity
     }

     ?pub_entity ff-map:longtermPopularity ?long_popularity .
     FILTER (?long_popularity > "1.0"^^xsd:double)
     BIND(IF(?long_popularity < "3.0"^^xsd:double , ?long_popularity * 1.5, ?long_popularity) as ?longterm_popularity) 

    ?pub_entity pub:preferredLabel ?entity_label .
    ?pub_entity pub:exactMatch ?mentioned_lod_entity.
      ?mentioned_lod_entity a owl:Thing .
}
GROUP BY ?mentioned_lod_entity ?entity_label ?longterm_popularity
ORDER BY DESC(?relative_popularity)
LIMIT 100